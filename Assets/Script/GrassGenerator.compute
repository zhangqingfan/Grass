// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

struct GrassConfig
{
    float pullToCenter;
    float sameDir;
};

struct GrassPosition
{
    float3 pos;
};

uint _Range;
float _Spacing;
float4x4 _VP_MATRIX;
int grassConfigBufferCount;

AppendStructuredBuffer<GrassPosition> worldPosBuffer;
StructuredBuffer<GrassConfig> grassConfigBuffer;

Texture2D<float4> voronoiRT;
SamplerState sampler_voronoiRT;

bool CameraCull(float3 worldPos)
{
    float4 clipPos = mul(_VP_MATRIX, float4(worldPos.xyz, 1));
    if (0 < clipPos.z && clipPos.z < clipPos.w &&
       -clipPos.w < clipPos.x && clipPos.x < clipPos.w &&
       -clipPos.w < clipPos.y && clipPos.y < clipPos.w)
    {
        return false;
    }
    return true;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if(id.x < _Range && id.y < _Range)
    {
        float2 uv = float2(id.x, id.y) / float2(_Range, _Range);
        float4 col = voronoiRT.SampleLevel(sampler_voronoiRT, uv, 0);
        float2 centerPosUV = float2(col.g, col.b);
        
        int idx = (int) col.r;
        idx = fmod(idx, grassConfigBufferCount);
        GrassConfig config = grassConfigBuffer[idx]; //TODO...
        uv = lerp(uv, centerPosUV, config.pullToCenter);
        
        float3 position = float3(uv.x * _Range * _Spacing, 0, uv.y * _Range * _Spacing);
        if (CameraCull(position) == true)
            return;
        
        GrassPosition gp;
        gp.pos = position;
        worldPosBuffer.Append(gp);            
    }
}
